
run = [
	insert(6),
	insert(3),
	insert(8),
	insert(1),
	insert(5),
	insert(7),
	insert(11),
	insert(9),
	insert(13)
	].

head = i(P), P = null.
head = i(P), run = [X | Y] :- head = i(P, X), run = Y.

% insert rules
Q = i(P, insert(X)), P = null :- int(X) | 
	Q = i(P), {p(P), v(V), l(L), r(R)}, V = X, L = i(LP), R = i(RP),
	LP = null, RP = null.
Q = i(P, insert(X, N)), P = null, M = i(MP, N), {p(MP), $p} :- int(X) | 
	Q = i(P), {p(P), v(V), l(L), r(R), next(N)}, V = X, 
	L = i(LP), R = i(RP), LP = null, RP = null,
	M = i(MP), {p(MP), prev(MPP), $p}, MPP = i(N). 
Q = i(P, insert(X)), {p(P), v(V), $p}, V = Y :- X =:= Y |
	Q = i(P), {p(P), v(V), $p}, V = Y.
Q = i(P, insert(X, N)), {p(P), v(V), $p}, V = Y, M = i(MP, N) :- X =:= Y |
	Q = i(P), {p(P), v(V), $p}, V = Y, M = i(MP).
Q = i(P, insert(X)), {p(P), v(V), l(L), $p}, V = Y, L = i(LP) :- X < Y |
	Q = i(P, O), {p(P), v(V), l(L), $p}, V = Y, L = i(LP, insert(X, O)).
Q = i(P, insert(X, N)), {p(P), v(V), l(L), $p}, V = Y, L = i(LP), 
	M = i(MP, N) :- X < Y | Q = i(P, O), {p(P), v(V), l(L), $p}, V = Y, 
	L = i(LP, insert(X, O)), M = i(MP).
Q = i(P, insert(X)), {p(P), v(V), l(L), r(R)}, V = Y, R = i(RP) :- X > Y |
	Q = i(P), {p(P), v(V), l(L), r(R)}, V = Y, R = i(RP, insert(X)).
Q = i(P, insert(X)), {p(P), v(V), l(L), r(R), prev(PV)}, V = Y, R = i(RP) :- X > Y |
	Q = i(P), {p(P), v(V), l(L), r(R), prev(PV)}, V = Y, R = i(RP, insert(X)).
Q = i(P, insert(X)), {p(P), v(V), l(L), r(R), next(NN), $p}, V = Y, R = i(RP),
	NNP = i(NN), {prev(NNP), $np}:- X > Y | Q = i(P), {p(P), v(V), l(L), r(R), $p},
	V = Y, R = i(RP, insert(X)), {$np}.
Q = i(P, insert(X, N)), {p(P), v(V), l(L), r(R)}, V = Y, R = i(RP),
	M = i(MP, N):- X > Y | Q = i(P), {p(P), v(V), l(L), r(R)}, V = Y,
	R = i(RP, insert(X, N)), M = i(MP, N).
Q = i(P, insert(X, N)), {p(P), v(V), l(L), r(R), prev(PV)}, V = Y, R = i(RP),
	M = i(MP, N):- X > Y | Q = i(P), {p(P), v(V), l(L), r(R), prev(PV)}, V = Y,
	R = i(RP, insert(X, N)), M = i(MP, N).
Q = i(P, insert(X, N)), {p(P), v(V), l(L), r(R), next(NN), $p}, V = Y, R = i(RP),
	M = i(MP, N), NNP = i(NN), {prev(NNP), $np} :- X > Y |
	Q = i(P), {p(P), v(V), l(L), r(R), $p}, V = Y, R = i(RP, insert(X, N)),
	M = i(MP, N), {$np}.

% search rules
Q = i(P, search(X)), {p(P), v(V), $p}, V = Y :- X =:= Y |
	Q = i(P), {p(P), v(V), $p}, V = Y, true(X).
Q = i(P, search(X)), {p(P), v(V), l(L), $p}, V = Y, L = i(LP) :- X < Y |
	Q = i(P), {p(P), v(V), l(L), $p}, V = Y, L = i(LP, search(X)).
Q = i(P, search(X)), {p(P), v(V), r(R), $p}, V = Y, R = i(RP) :- X > Y |
	Q = i(P), {p(P), v(V), r(R), $p}, V = Y, R = i(RP, search(X)).
Q = i(P, search(X)), P = null :- Q = i(P), P = null, false(X). 

% delete rules
Q = i(P, delete(X)), {p(P), v(V), l(L), r(R)}, V = Y, L = i(LP), R = i(RP),
	LP = null, RP = null :- X =:= Y | Q = i(P), P = null.
Q = i(P, delete(X)), {p(P), v(V), l(L), r(R), next(N)}, V = Y,
	L = i(LP), R = i(RP), LP = null, RP = null, N = i(NP), {prev(NP), $p} :-
	X =:= Y | Q = i(P), P = null, {$p}.
Q = i(P, delete(X)), {p(P), v(V), l(L), r(R)}, V = Y, L = i(LP), R = i(RP),
	LP = null, {p(RP), v(RV), $rp} :- X =:= Y |
	Q = i(RP), {p(RP), v(RV), $rp}.
Q = i(P, delete(X)), {p(P), v(V), l(L), r(R), next(N)}, V = Y,
	L = i(LP), R = i(RP), LP = null, {p(RP), v(RV), $rp},
	N = i(NP), {prev(NP, $p)} :- X =:= Y | Q = i(RP), {p(RP), v(RV), $rp}.
Q = i(P, delete(X)), {p(P), v(V), l(L), r(R)}, V = Y, L = i(LP), R = i(RP),
	{p(LP), v(LV), $lp}, RP = null :- X =:= Y |
	Q = i(LP), {p(LP), v(LV), $lp}.
Q = i(P, delete(X)), {p(P), v(V), l(L), r(R)}, V = Y, L = i(LP), R = i(RP),
	{p(LP), v(LV), $lp}, {p(RP), v(RV), $rp} :- X =:= Y |
	Q = i(P, delete(X, searchMax(S))), {p(P), v(V), l(L), r(R)}, V = Y,
	L = i(LP, S), R = i(RP), {p(LP), v(LV), $lp}, {p(RP), v(RV), $rp}.
Q = i(P, delete(X, searchMax(S))), {p(P), v(V), l(L), r(R)}, V = Y, M = i(LP, S),
	{p(LP), v(LV), l(LL), r(LR)}, LL = i(LLP), LR = i(null) :- int(X), int(Y) |
	Q = i(P), {p(P), v(LV), l(L), r(R)}, M = i(LLP).
Q = i(P, S), D = searchMax(S), {p(P), r(R), $p}, R = i(RP), {p(RP), $rp} :-
	Q = i(P), {p(P), r(R), $p}, R = i(RP, S), D = searchMax(S), {p(RP), $rp}.
Q = i(P, delete(X)), {p(P), v(V), l(L), $p}, V = Y, L = i(LP) :- X < Y |
	Q = i(P), {p(P), v(V), l(L), $p}, V = Y, L = i(LP, delete(X)).
Q = i(P, delete(X)), {p(P), v(V), r(R), $p}, V = Y, R = i(RP) :- X > Y |
	Q = i(P), {p(P), v(V), r(R), $p}, V = Y, R = i(RP, delete(X)).
Q = i(P, delete(X)), P = null :- int(X) | Q = i(P), P = null.